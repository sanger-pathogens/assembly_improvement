#!/usr/bin/env perl
package Bio::AssemblyImprovement::Bin::ImproveAssembly;
# ABSTRACT: Given an assembly, some reads and optionally a reference, reduce the number of contigs and fill gaps.
# PODNAME: improve_assembly
=head1 SYNOPSIS

Given an assembly, some reads and optionally a reference, reduce the number of contigs and fill gaps.

   # Improve the assembly without a reference
   improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq

   # Provide a reference
   improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq  -c my_reference.fa

   # Gzipped input files are accepted
   improve_assembly -a contigs.fa.gz -f 123_1.fastq.gz -r 123_2.fastq.gz

   # Insert size defaults to 250 if not specified
   improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -i 3000

   # Output to a specific directory
   improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -o my_directory

   # This help message
   improve_assembly -h

=cut


BEGIN { unshift( @INC, '../lib' ) }
use Moose;
use Getopt::Long;
use File::Copy;
use Cwd;
use Cwd 'abs_path';
use File::Path qw(make_path);
use Bio::AssemblyImprovement::Scaffold::SSpace::PreprocessInputFiles;
use Bio::AssemblyImprovement::Scaffold::SSpace::Iterative;
use Bio::AssemblyImprovement::FillGaps::GapFiller::Iterative;
use Bio::AssemblyImprovement::Abacas::Iterative;
use Bio::AssemblyImprovement::Validate::Executable;
use Bio::AssemblyImprovement::Util::FastaTools;
use Bio::AssemblyImprovement::Util::FastqTools;
use Bio::AssemblyImprovement::Util::OrderContigsByLength;

my ( $assembly_file, $forward_reads_file, $reverse_reads_file, $insert_size, $scaffolder_exec, $abacas_exec,$gapfiller_exec, $debug,$reference,$output_directory, $min_final_contig_length, $min_perc_to_not_filter_final_contigs, $help );

GetOptions(
    'a|assembly=s'        => \$assembly_file,
    'f|forward_fastq=s'   => \$forward_reads_file,
    'r|reverse_fastq=s'   => \$reverse_reads_file,
    'c|reference=s'       => \$reference,
    'i|insert_size=i'     => \$insert_size,
    's|scaffolder_exec=s' => \$scaffolder_exec,
    'b|abacas_exec=s'     => \$abacas_exec,
    'g|gapfiller_exec=s'  => \$gapfiller_exec,
    'd|debug'             => \$debug,
    'o|output_directory=s' => \$output_directory,
    'l|min_final_contig_length' => \$min_final_contig_length,
    'p|min_perc_to_not_filter_final_contigs' => \$min_perc_to_not_filter_final_contigs,
    'h|help'              => \$help,
);

( defined($assembly_file) && defined($forward_reads_file) && defined($reverse_reads_file) && ( -e $assembly_file ) && ( -e $forward_reads_file ) && ( -e $reverse_reads_file ) && !$help ) or die <<USAGE;
Usage: improve_assembly [options] -a <FASTA> -f <FASTQ> -r <FASTQ> -s <EXEC> -g <EXEC>
Given an assembly in FASTA format and paired ended reads in FASTQ format, output a scaffolded and gapfilled assembly.

Required: 	
    -a STR  assembly FASTA(.gz) file
    -f STR  forward reads FASTQ(.gz) file
    -r STR  reverse reads FASTQ(.gz) file
    -s STR  path to SSPACE executable
    -g STR  path to GapFiller executable
Options:    
    -c STR  reference FASTA(.gz) file
    -i INT  insert size [250]
    -b STR  path to abacas.pl executable
    -o STR  output directory [.]
    -l INT  Minimum final contig length [300]
    -p INT  Only filter if this percentage of bases left [95]
    -d      debug output []
    -h      print this message and exit

# Improve the assembly without a reference
improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq

# Provide a reference
improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq  -c my_reference.fa

# Gzipped input files are accepted
improve_assembly -a contigs.fa.gz -f 123_1.fastq.gz -r 123_2.fastq.gz

# Insert size defaults to 250 if not specified
improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -i 3000

# Minimum length of final sequences defaults to 300 if not specified
improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -l 300

# Filtering only used if at least 95% of bases remain after filtering. Change with -p.
# improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -p 95

# Output to a specific directory
improve_assembly -a contigs.fa -f 123_1.fastq -r 123_2.fastq -o my_directory

# This help message
improve_assembly -h

USAGE

# abs_path to inputs
$assembly_file = abs_path($assembly_file) if(defined $assembly_file);
$forward_reads_file = abs_path($forward_reads_file) if(defined $forward_reads_file);
$reverse_reads_file = abs_path($reverse_reads_file) if(defined $reverse_reads_file);

$debug           ||= 0;
$insert_size     ||= 250;
$min_final_contig_length ||= 300;
$min_perc_to_not_filter_final_contigs ||= 95;

#Default executables
my $scaffolder_default = '/software/pathogen/external/apps/usr/local/SSPACE-BASIC-2.0_linux-x86_64/SSPACE_Basic_v2.0.pl';
my $gapfiller_default  = '/software/pathogen/external/apps/usr/local/GapFiller_v1-10_linux-x86_64/GapFiller.pl';
my $abacas_default     = '/software/pathogen/internal/prod/bin/abacas.pl';

# If executables not specified or do not exist, use the default
$scaffolder_exec = Bio::AssemblyImprovement::Validate::Executable->new()->check_executable_and_set_default($scaffolder_exec, $scaffolder_default);
$gapfiller_exec = Bio::AssemblyImprovement::Validate::Executable->new()->check_executable_and_set_default($gapfiller_exec, $gapfiller_default);
$abacas_exec = Bio::AssemblyImprovement::Validate::Executable->new()->check_executable_and_set_default($abacas_exec, $abacas_default);

$output_directory ||= getcwd();
$output_directory  = abs_path($output_directory);
make_path($output_directory);

my @input_files = ( $forward_reads_file, $reverse_reads_file );

my $preprocess_input_files = Bio::AssemblyImprovement::Scaffold::SSpace::PreprocessInputFiles->new(
    input_files    => \@input_files,
    input_assembly => $assembly_file,
    reference      => $reference,
);
my $process_input_files_tmp_dir_obj = $preprocess_input_files->_temp_directory_obj();

# scaffold and extend contigs
my $scaffolding_obj = Bio::AssemblyImprovement::Scaffold::SSpace::Iterative->new(
    input_files     => $preprocess_input_files->processed_input_files,
    input_assembly  => $preprocess_input_files->processed_input_assembly,
    insert_size     => $insert_size,
    scaffolder_exec => $scaffolder_exec,
    debug           => $debug,
    output_base_directory => $output_directory
);
$scaffolding_obj->run();

my $scaffolding_output = $scaffolding_obj->final_output_filename;
my $fill_gaps_input = $scaffolding_output;

# order contigs on an assembly
if(defined($reference))
{
  $scaffolding_obj = Bio::AssemblyImprovement::Abacas::Iterative->new(
    reference      => $preprocess_input_files->processed_reference,
    input_assembly => $scaffolding_output,
    abacas_exec    => $abacas_exec,
    debug          => $debug,
    output_base_directory => $output_directory
  );
  $fill_gaps_input = $scaffolding_obj->run();
}


# fill gaps
my $fill_gaps_obj = Bio::AssemblyImprovement::FillGaps::GapFiller::Iterative->new(
    input_files     => $preprocess_input_files->processed_input_files,
    input_assembly  => $fill_gaps_input,
    insert_size     => $insert_size,
    gap_filler_exec => $gapfiller_exec,
    debug           => $debug,
    _output_prefix  => 'gapfilled',
    output_base_directory => $output_directory
)->run();

# remove tiny contigs
my $fastq_processor = Bio::AssemblyImprovement::Util::FastqTools->new(input_filename => $forward_reads_file);
my $read_length = $fastq_processor->first_read_length();
my $fill_gaps_output = $fill_gaps_obj->final_output_filename;
my $fasta_processor = Bio::AssemblyImprovement::Util::FastaTools->new(input_filename => $fill_gaps_output);
$fasta_processor->remove_small_contigs($read_length, 0);
my $remove_tiny_contigs_output = $fasta_processor->output_filename;

# remove small contigs
my $fasta_processor2 = Bio::AssemblyImprovement::Util::FastaTools->new(input_filename => $remove_tiny_contigs_output);
$fasta_processor2->remove_small_contigs($min_final_contig_length, $min_perc_to_not_filter_final_contigs);
move($fasta_processor2->output_filename, "$output_directory/scaffolds.scaffolded.gapfilled.length_filtered.fa");

# sort contigs by length
my $order_contigs = Bio::AssemblyImprovement::Util::OrderContigsByLength->new( input_filename  => "$output_directory/scaffolds.scaffolded.gapfilled.length_filtered.fa" );
$order_contigs->output_filename("$output_directory/scaffolds.scaffolded.gapfilled.length_filtered.sorted.fa");
$order_contigs->run();
